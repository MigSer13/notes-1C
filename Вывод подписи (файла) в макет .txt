//в макете надо добавить Таблица-Рисунки-Картинки-Добавить(Любую, можно пустую). 
//Далее на макете именуем её ПодписьРуководителя (вставка с помощью  Область.Рисунки.ПодписьРуководителя.Картинка)

//ФОРМА
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    Если Запись.ИсходныйКлючЗаписи.ФизЛицо.Пустая() Тогда    
        Запись.ФизЛицо = Неопределено;
        СсылкаНаКартинку = "";
    Иначе
        СсылкаНаКартинку = ПолучитьНавигационнуюСсылку(Запись.ИсходныйКлючЗаписи, "ФайлХранилище");    
    КонецЕсли;                                                                                         
КонецПроцедуры
 
&НаКлиенте
Процедура УстановитьКартинку(Команда)
    СтандартнаяОбработка = Ложь; 
    Режим = РежимДиалогаВыбораФайла.Открытие; 
    ДиалогОткрытия = Новый ДиалогВыбораФайла(Режим); 
    ДиалогОткрытия.ПолноеИмяФайла = ""; 
    Фильтр = "Файл Jpg (*.jpg)|*.jpg"; 
    ДиалогОткрытия.Фильтр = Фильтр; 
    ДиалогОткрытия.МножественныйВыбор = Ложь; 
    ДиалогОткрытия.Заголовок = "Выберите файл для загрузки"; 
    ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЗагрузкиФайла",ЭтаФорма); 
    ДиалогОткрытия.Показать(ОписаниеОповещения);    
КонецПроцедуры
 
&НаКлиенте 
Процедура ПослеЗагрузкиФайла(ВыбранныйФайл, ДопПараметр) Экспорт 
    Если ВыбранныйФайл = Неопределено Тогда 
        Возврат; 
    КонецЕсли; 
    ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПомещенияФайла", ЭтаФорма); 
    НачатьПомещениеФайла(ОписаниеОповещения,, ВыбранныйФайл[0], Ложь, УникальныйИдентификатор); 
КонецПроцедуры
 
&НаКлиенте 
Процедура ПослеПомещенияФайла(Результат, Адрес, ВыбранноеИмяФайла,ДопПараметры) Экспорт 
    Если Не Результат Тогда 
        Возврат; 
    КонецЕсли; 
    СсылкаНаКартинку = Адрес; 
    Модифицированность = Истина; 
КонецПроцедуры
 
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
    Если ЭтоАдресВременногоХранилища(СсылкаНаКартинку)  Тогда 
        ФайлКартинки = ПолучитьИзВременногоХранилища(СсылкаНаКартинку); 
        ТекущийОбъект.ФайлХранилище = Новый ХранилищеЗначения(ФайлКартинки);                    
        УдалитьИзВременногоХранилища(СсылкаНаКартинку); 
        СсылкаНаКартинку = ПолучитьНавигационнуюСсылку(Запись.ИсходныйКлючЗаписи, "ФайлХранилище"); 
    КонецЕсли;
КонецПроцедуры
 
//ПОЛУЧЕНИЕ ДАННЫХ в модуле
Функция цка_ПолучитьПодписи(Организация, РуководительФизическоеЛицо, ГлавныйБухгалтерФизическоеЛицо)
    СтруктураПодписиОтвЛиц = Новый Структура("ПодписьРуководителя, ПодписьБухгалтера", Неопределено, Неопределено);
        
    Запрос = Новый Запрос;
    Запрос.Текст = 
        "ВЫБРАТЬ
        |    цка_ПодписиОтветственныхЛиц.ФизЛицо КАК ФизЛицо,
        |    цка_ПодписиОтветственныхЛиц.ФайлХранилище КАК КартинкаПодписьРуководителя
        |ИЗ
        |    РегистрСведений.цка_ПодписиОтветственныхЛиц КАК цка_ПодписиОтветственныхЛиц
        |ГДЕ
        |    цка_ПодписиОтветственныхЛиц.ФизЛицо = &ФИОРуководителя
        |    И цка_ПодписиОтветственныхЛиц.Организация = &Организация
        |;
        |ВЫБРАТЬ
        |    цка_ПодписиОтветственныхЛиц.ФизЛицо КАК ФизЛицо,
        |    цка_ПодписиОтветственныхЛиц.ФайлХранилище КАК КартинкаПодписьБухгалтера
        |ИЗ
        |    РегистрСведений.цка_ПодписиОтветственныхЛиц КАК цка_ПодписиОтветственныхЛиц
        |ГДЕ
        |    цка_ПодписиОтветственныхЛиц.ФизЛицо = &ФИОБухгалтера
        |    И цка_ПодписиОтветственныхЛиц.Организация = &Организация";    
    Запрос.УстановитьПараметр("Организация",      Организация);
    Запрос.УстановитьПараметр("ФИОРуководителя", РуководительФизическоеЛицо);    
    Запрос.УстановитьПараметр("ФИОБухгалтера",      ГлавныйБухгалтерФизическоеЛицо);
    Пакеты = Запрос.ВыполнитьПакет();
    ДанныеРуководителя = Пакеты[0].Выбрать();
    Если ДанныеРуководителя.Следующий()Тогда    
        СтруктураПодписиОтвЛиц.Вставить("ПодписьРуководителя", ДанныеРуководителя.КартинкаПодписьРуководителя);
    КонецЕсли;  
            
    ДанныеБухгалтера = Пакеты[1].Выбрать();
    Если ДанныеБухгалтера.Следующий()Тогда    
        СтруктураПодписиОтвЛиц.Вставить("ПодписьБухгалтера", ДанныеБухгалтера.КартинкаПодписьБухгалтера);
    КонецЕсли; 
    
    Возврат СтруктураПодписиОтвЛиц;
КонецФункции // ()
 
//ВСТАВКА 
    СтруктураПодписиОтвЛиц = цка_ПолучитьПодписи(ДанныеПечати.Организация, ДанныеПечати.РуководительФизическоеЛицо, ДанныеПечати.ГлавныйБухгалтерФизическоеЛицо);
    Если СтруктураПодписиОтвЛиц.ПодписьРуководителя <> Неопределено Тогда    
        ПодписьРуководителя = Новый Картинка(СтруктураПодписиОтвЛиц.ПодписьРуководителя.Получить(), Истина);
        Область.Рисунки.ПодписьРуководителя.Картинка = ПодписьРуководителя;
    КонецЕсли;
    Если СтруктураПодписиОтвЛиц.ПодписьБухгалтера <> Неопределено Тогда    
        ПодписьБухгалтера = Новый Картинка(СтруктураПодписиОтвЛиц.ПодписьБухгалтера.Получить(), Истина);
        Область.Рисунки.ПодписьБухгалтера.Картинка = ПодписьБухгалтера;
    КонецЕсли;    