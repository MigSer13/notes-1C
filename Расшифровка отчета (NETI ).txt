&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
    
    СхемаКомпоновки = ПоместитьВоВременноеХранилище(ОтчетОбъект.СхемаКомпоновкиДанных, Новый УникальныйИдентификатор);
    
КонецПроцедуры

 

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
    
    Перем ВыполненноеДействие, ПараметрВыполненногоДействия;
    
    Если ТипЗнч(Расшифровка) <> Тип("ИдентификаторРасшифровкиКомпоновкиДанных") Тогда
        Возврат;
    КонецЕсли;
    
    СтандартнаяОбработка = Ложь;
    
    Если ПустаяСтрока(СхемаКомпоновки)
        Или Не ЭтоАдресВременногоХранилища(СхемаКомпоновки) Тогда
        Возврат;
    КонецЕсли;
    
    ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(ДанныеРасшифровки, Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновки));
    
    ДоступныеДействия = Новый Массив;
    ДоступныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение);
    ДоступныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.Расшифровать);
    ДоступныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.Упорядочить);
    
    ДополнительныеПункты = Новый СписокЗначений;
    ДополнительныеПункты.Добавить("ВывестиВСообщение","Вывести в сообщение");
            
    ОбработкаРасшифровки.ВыбратьДействие(Расшифровка,ВыполненноеДействие,ПараметрВыполненногоДействия, ДоступныеДействия,ДополнительныеПункты);
    
    Если ВыполненноеДействие <> ДействиеОбработкиРасшифровкиКомпоновкиДанных.Нет
        И ПараметрВыполненногоДействия <> Неопределено Тогда   // требуется сформировать отчет с новыми настройками
        
        Если ВыполненноеДействие = ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение Тогда
            ОткрытьЗначение(ПараметрВыполненногоДействия);
        Иначе
            ОписаниеРасшифровки = Новый ОписаниеОбработкиРасшифровкиКомпоновкиДанных(ДанныеРасшифровки,Расшифровка,ПараметрВыполненногоДействия);
            
            ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(ДанныеРасшифровки, Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновки));
            
            ПараметрыФормы = Новый Структура("КлючНазначенияИспользования,СформироватьПриОткрытии,Расшифровка","Расшифровка",Истина,ОписаниеРасшифровки);
            
            ОткрытьФорму("Отчет.ПрограммнаяРасшифрокаОтчета.Форма.ФормаОтчета",ПараметрыФормы,ЭтаФорма);
            
        КонецЕсли;
        
    ИначеЕсли ВыполненноеДействие = "ВывестиВСообщение"    Тогда
        
        ПолучитьЗначениеРасшифровки(Расшифровка);    
        
    КонецЕсли;    
                                    
КонецПроцедуры

 

&НаСервере
Процедура ПолучитьЗначениеРасшифровки(Расшифровка)
    
    мДанныеРасшифровки = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
    
    ЭлементРасшифровки = мДанныеРасшифровки.Элементы[Расшифровка];
    
    Если ТипЗнч(ЭлементРасшифровки)  = Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля") Тогда
    
        ПоляРасшифровки = ЭлементРасшифровки.ПолучитьПоля();
        
        Для каждого ПолеРасшифроки Из ПоляРасшифровки Цикл
        
            Сообщить(ПолеРасшифроки.Значение);
        
        КонецЦикла;
    
    КонецЕсли;
    
КонецПроцедуры // ПолучитьЗначениеРасшифровки()

Где "СхемаКомпоновки" это реквизит формы с типом "Строка", в которую будет помещен адрес во временом хранилище для схемы компновки данных.