
&После("ДобавитьКомандыПечати")
Процедура Расш1_ДобавитьКомандыПечати(КомандыПечати)
    КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "цка_ПФ_ЛимитноЗаборнаяПоРемонтам";
	КомандаПечати.Представление = НСтр("ru = 'Лимитно-заборная ведомость'");
	КомандаПечати.Картинка = БиблиотекаКартинок.Печать;
	КомандаПечати.Порядок = 2;
КонецПроцедуры

&После("Печать")
Процедура Расш1_Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода)    
    Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "цка_ПФ_ЛимитноЗаборнаяПоРемонтам") Тогда
    	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, 
                    "цка_ПФ_ЛимитноЗаборнаяПоРемонтам", 
                    НСтр("ru='Лимитно-заборная ведомость'"), 
                    цка_ПечатьЛимитноЗаборнойВедомости(МассивОбъектов, ОбъектыПечати));	
    КонецЕсли;
КонецПроцедуры

Функция цка_ПечатьЛимитноЗаборнойВедомости(МассивОбъектов, ОбъектыПечати)
    ТабличныйДокумент = Новый ТабличныйДокумент;
    
    Макет = ПолучитьОбщийМакет("цка_ПФ_ЛимитноЗаборнаяПоРемонтам");
    
    ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
    ОбластьСтрокаТаблицы = Макет.ПолучитьОбласть("СтрокаТаблицы");
    ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
    
    Запрос = Новый Запрос;
    Запрос.Текст = "ВЫБРАТЬ
    |    	АпкВыполнениеРемонтаТСМатериалыИРаботы.Ссылка.Организация.Наименование КАК Организация,
    |    	АпкВыполнениеРемонтаТСМатериалыИРаботы.Ссылка.Подразделение.Наименование КАК Подразделение,
    |    	АпкВыполнениеРемонтаТСМатериалыИРаботы.Ссылка.цка_ОтветственныйИнженер.Наименование КАК ИнженерПолучатель,
    |    	АпкВыполнениеРемонтаТСМатериалыИРаботы.Ссылка.ТранспортноеСредство.Наименование КАК ТранспортноеСредство,
    |    	АпкВыполнениеРемонтаТСМатериалыИРаботы.Ссылка.ТранспортноеСредство.Код КАК ГосНомер,
    |    	АпкВыполнениеРемонтаТСМатериалыИРаботы.Ссылка.ТранспортноеСредство.Марка КАК ТранспортноеСредствоМаркаИГосНомер,
    |    	АпкВыполнениеРемонтаТСМатериалыИРаботы.цка_ДокПеремещенияТоваров КАК ДокПеремещенияТоваров,
    |    	АпкВыполнениеРемонтаТСМатериалыИРаботы.НомерСтроки КАК НомерСтроки,
    |    	АпкВыполнениеРемонтаТСМатериалыИРаботы.Номенклатура.Наименование КАК Номенклатура,
    |    	АпкВыполнениеРемонтаТСМатериалыИРаботы.Номенклатура.Код КАК НоменклатураКод,
    |    	АпкВыполнениеРемонтаТСМатериалыИРаботы.Характеристика.Наименование КАК Характеристика,
    |    	АпкВыполнениеРемонтаТСМатериалыИРаботы.Серия.Наименование КАК Серия,
    |    	АпкВыполнениеРемонтаТСМатериалыИРаботы.Количество КАК КоличествоКВыдаче,
    |    	АпкВыполнениеРемонтаТСМатериалыИРаботы.Упаковка.Наименование КАК ЕдИзм
    |    ИЗ
    |    	Документ.АпкВыполнениеРемонтаТС.МатериалыИРаботы КАК АпкВыполнениеРемонтаТСМатериалыИРаботы
    |    ГДЕ
    |    	АпкВыполнениеРемонтаТСМатериалыИРаботы.Ссылка В(&МассивОбъектов)
    |    ИТОГИ ПО
    |    	ДокПеремещенияТоваров";	
    Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
    ВыборкаДокументПеремещения = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);	
    Пока ВыборкаДокументПеремещения.Следующий() Цикл
        Выборка = ВыборкаДокументПеремещения.Выбрать();
        ШапкаВыведена = Ложь;        
        Пока Выборка.Следующий() Цикл
            Если НЕ ШапкаВыведена Тогда	
                ОбластьЗаголовок.Параметры.Заполнить(Выборка);
                ТабличныйДокумент.Вывести(ОбластьЗаголовок);
                ШапкаВыведена = Истина;                
            КонецЕсли;  
            
            ОбластьСтрокаТаблицы.Параметры.Заполнить(Выборка);
            ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);                                      
        КонецЦикла; 
        
        ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
    КонецЦикла; 
    
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_цка_Лимитка";
	ТабличныйДокумент.ОтображатьЗаголовки 	= Ложь;
	ТабличныйДокумент.ОтображатьСетку     	= Ложь;
	ТабличныйДокумент.ЧерноБелыйПросмотр  	= Истина;
	ТабличныйДокумент.ЧерноБелаяПечать    	= Истина;
	ТабличныйДокумент.Автомасштаб         	= Истина;
	ТабличныйДокумент.ОриентацияСтраницы  	= ОриентацияСтраницы.Портрет;
    
    Возврат ТабличныйДокумент;
КонецФункции // ()
 


